plugins {
    id 'idea'
    id 'java-library'
    id 'net.neoforged.gradle.userdev' version '7.0.107'
    id 'me.modmuss50.mod-publish-plugin' version '0.6.3'
}

version = "${version}-neoforge"
jarJar.enable()
base.archivesName = mod_id

if (file('src/main/resources/META-INF/accesstransformer.cfg').exists()) {
    minecraft.accessTransformers.file file('src/main/resources/META-INF/accesstransformer.cfg')
}
runs {
    configureEach {
        modSource project.sourceSets.main
    }
    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }
    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }
    gameTestServer {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }
    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"
    compileOnly project(":common")

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    jarJar "com.jahirtrap:configlibtxf:${configlibtxf_version}-neoforge"
}

Spec<Task> notNeoTask = { Task it -> !it.name.startsWith("neo") } as Spec<Task>
tasks.withType(JavaCompile).matching(notNeoTask).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
tasks.withType(Javadoc).matching(notNeoTask).configureEach {
    source(project(":common").sourceSets.main.allJava)
}

def resourceTargets = ['META-INF/neoforge.mods.toml', 'pack.mcmeta']
def replaceProperties = [
        minecraft_version: minecraft_version, minecraft_version_range: neoforge_minecraft_version_range,
        neoforge_version: neoforge_version, neoforge_version_range: neoforge_version_range,
        loader_version_range: neoforge_loader_version_range,
        mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: version,
        mod_author: mod_author, mod_description: mod_description,
        mod_url: mod_url, mod_issues: mod_issues, side: neoforge_side
]

processResources {
    inputs.properties replaceProperties
    replaceProperties.put 'project', project

    filesMatching(resourceTargets) {
        expand replaceProperties
    }
    exclude '**/data/fabric/**', '**/data/forge/**'
    from project(":common").sourceSets.main.resources
}

tasks.jarJar.setArchiveClassifier('')
tasks.jar.setArchiveClassifier('slim')
tasks.jarJar.doLast { delete(jar.archiveFile) }
jar.finalizedBy('jarJar')

publishMods {
    file = tasks.jarJar.archiveFile
    displayName = "${mod_name} ${project.version}"
    type = STABLE
    modLoaders.addAll("neoforge")
    changelog = rootProject.file("CHANGELOG.md").text
    def gameVersions = ["1.20.5", "1.20.6"]

    curseforge {
        accessToken = System.getenv("CURSEFORGE_TOKEN")
        projectId = "883384"
        minecraftVersions.addAll(gameVersions)
    }

    modrinth {
        accessToken = System.getenv("MODRINTH_TOKEN")
        projectId = "N56hoqkq"
        minecraftVersions.addAll(gameVersions)
    }
}

["publishCurseforge", "publishModrinth", "publishMods"].each { task ->
    tasks.named(task) {
        mustRunAfter(":forge:$task")
    }
}